// Kusto Query:  This query gets it down to 2 Domains.  The only extra piece missing is taking the domains and looking at the fully deleted content to determine the file that was partially deleted.
// We know there are Read, Create, Delete-Complete, Delete-Partial actions
let GetAction = (EventText: string) { 
    let action = substring(EventText, 0, indexof(EventText, " "));
    let subaction = iff(indexof( EventText, "partially")>0, "-Partial", iff(indexof(EventText, "completely")>0, "-Full", ""));
    strcat(action, subaction)
};
let GetDomainAddress = (EventText: string) {
    let endString = substring(EventText, 0, indexof(EventText, "blob.core.windows.net")-1);
    let whackIndex = indexof(endString,"//")+2;
    substring(endString,whackIndex, strlen(endString)-whackIndex)
};
let GetURL = (EventText: string) {
    let endString = substring(EventText, indexof(EventText, "http"));
    let whackIndex = indexof(endString,"'");
    substring(endString,0,whackIndex)
};
let ExtendStorageArchiveLogs = StorageArchiveLogs // Lets get all of the created videos that have a start and end date that could have weekly drops
| extend ActionItem = GetAction(EventText), DomainItem = GetDomainAddress(EventText), ContentURL = GetURL(EventText);
let DeleteDomains = ExtendStorageArchiveLogs
| where ActionItem contains "Delete"
| summarize count(), countPartial=countif(ActionItem =="Delete-Partial"), countFull=countif(ActionItem=="Delete-Full") by ContentURL, DomainItem
| project TotalCount= count_, TotalPartialDelete=countPartial, TotalFullDelete=countFull, ContentURL, DomainItem;
let PartialDeleteDomains = DeleteDomains
| where TotalFullDelete == 0
| summarize countPartialDeletes = count() by DomainItem
| project DomainItem, countPartialDeletes;
PartialDeleteDomains
| join kind=inner ExtendStorageArchiveLogs on $left.DomainItem == $right.DomainItem
| summarize MinPostDate = minif(Timestamp,ActionItem=="Create"), MaxPostDate = maxif(Timestamp,ActionItem=="Create"), count(),TotalReads = countif(ActionItem=="Read"),PostedVideos = countif(ActionItem=="Create"), FullDeletedVideos = countif(ActionItem=="Delete-Full"), PartialDeletedVideos = countif(ActionItem=="Delete-Partial") by DomainItem
| project DomainItem, MinPostDate, MaxPostDate, PostingDifference = datetime_diff('day', MaxPostDate, MinPostDate), AverageReads = TotalReads/PostedVideos, PostedVideos, PartialDeletedVideos, FullDeletedVideos, TotalReads
| where PostingDifference >= 20 and PostedVideos between (4 .. 10) and PartialDeletedVideos > 0 and AverageReads > 1000;  // Weekly post with 1 month there should be at least 28 days between the 1st and last post

